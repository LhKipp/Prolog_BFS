cmake_minimum_required(VERSION 3.13)
project(prolog_bfs)

# set where the build output should go
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/build)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/build)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/build)

set(CMAKE_CXX_STANDARD 17)

# this is not required anymore because of -s USE_BOOST_HEADERS=1 flag
#FIND_PACKAGE(Boost REQUIRED )
#INCLUDE_DIRECTORIES( ${Boost_INCLUDE_DIR} )

file(GLOB_RECURSE prolog_bfs_src
    "src/wasm/*"
    "src/wam/*.h"
    "src/wam/*.cpp"
)

add_executable(prolog_bfs ${prolog_bfs_src})

#make include from src possible
target_include_directories(prolog_bfs PRIVATE ${PROJECT_SOURCE_DIR}/src)

# no need to mention boost libs anymore because of -s USE_BOOST_HEADERS=1
TARGET_LINK_LIBRARIES( prolog_bfs LINK_PUBLIC )

##########################
# compile and link options
##########################

# FLAG EXPLANATION
# --bind enable embind
# --emrun optimize for use with emrun (server to run the code)
# DISABLE_EXCEPTION_CATCHING=0 to allow exceptions *within* the c++ code to be caught.
#   Exceptions are disabled by default because of performance. We need them though.
# USE_BOOST_HEADERS=1 automatically retrieve the boost headers and include them
# MODULARIZE=1 prevent emscripten from polluting JS global scope. Put everything into EXPORT_NAME
# EXPORT_NAME specify the name of the namespace for MODULARIZE
# ALLOW_MEMORY_GROWTH Allow memory to grow to size defined by MAXIMUM_MEMORY

# Use "SHELL:" to prevent de-duplication of the -s flags! See https://stackoverflow.com/questions/60552844/how-do-i-migrate-from-compile-flags-to-target-compile-options-in-cmake/60553977#60553977

target_compile_options(prolog_bfs PUBLIC "SHELL:-s USE_BOOST_HEADERS=1" "SHELL:-s DISABLE_EXCEPTION_CATCHING=0")
target_link_options(prolog_bfs PUBLIC --bind --emrun "SHELL:-s USE_BOOST_HEADERS=1" "SHELL:-s DISABLE_EXCEPTION_CATCHING=0" "SHELL:-s MODULARIZE=1" "SHELL:-s EXPORT_NAME=\"'EmscriptenModule'\"" "SHELL:-s ALLOW_MEMORY_GROWTH=1" "SHELL:-s MAXIMUM_MEMORY=500MB")